#!/usr/bin/perl

use Parser;
use Evaluator;
use Term::ReadLine;
use Getopt::Long;
use Pod::Usage;

our $VERSION = "0.0.1";

our $help = 0;
our $prompt = "mcalc>";
our $silent=0;
our $verbose = 0;
our $version = 0;

# Hier geht es los!
our $term;
our $evaluator = Evaluator->new();

sub printWelcome {
  print "Welcome to mcalc v$VERSION\n"
}

sub printVersion {
  print "mcalc v$VERSION by Marc Hauptmann\n"
}

sub batch {
  calculate($_[0]);

  exit;
}

sub calculate {
  my $input = $_[0];

  eval {
    # parsen
    my $parser = Parser->new();
    my $tree = $parser->parse($input);

    # auswerten
    my $result = $evaluator->evaluate(\$tree);

    printf "$result\n";
  };
  if (my $error = $@) {
    print $error;
  }
}

sub complete {
  my ($text, $line, $start) = @_;
  my $attribs = $term->Attribs;

  $attribs->{completion_suppress_append} = 1;

  @completions = $evaluator->getCompletions($line);

  foreach $val ("quit", "exit", "bye") {
    if($val =~ /^$line/) {
      push @completions, $val;
    }
  }

  return @completions;
}

GetOptions(
	   "help" => \$help,
	   "prompt|p=s" => \$prompt,
	   "silent|s" => \$silent,
	   "version" => \$version,
	   "<>" => \&batch
);

if($help) {
  pod2usage( -verbose => 1 );
}
elsif($version) {
  printVersion();
}
else {
  if (!$silent) {
    printWelcome();
  }

  $term = Term::ReadLine->new("mcalc");
  my $attribs = $term->Attribs;

  $attribs->{completion_function} = \&complete;
  $attribs->{completer_word_break_characters} = "+-*/^()";

  while (1) {
    my $input = $term->readline("$prompt ");

    # Leerzeichen entfernen
    $input =~ s/\s//g;

    # Sonderbehandlung f√ºr 'quit'
    if ($input =~ /^(quit|bye|exit)$/) {
      if(!$silent) {
	print "bye :)\n";
      }

      exit;
    }

    calculate($input);
  }
}

__END__

=pod

=encoding utf-8

=head1 NAME

mcalc - a simple calculcator for the terminal

=head1 SYNOPSIS

B<mcalc> [I<OPTIONS>] [EXPRESSION]

=head1 DESCRIPTION

B<mcalc> is a simple calculator for the terminal. By default it presents a prompt for evaluating mathematical expressions. See L</"EXPRESSIONS"> for further information about the syntax.

If I<EXPRESSION> is submitted, the result is directly printed to the standard output and B<mcalc> terminates.


=head1 OPTIONS

=over 8

=item B<--help>

Print help and exit

=item B<-p> I<STRING>, B<--prompt> I<STRING>

Uses I<STRING> as command promt. The standard prompt is I<mcalcE<gt>>.

=item B<-s>, B<--silent>

Does not print messages at start or exit.

=item B<--version>

Prints the version number of B<mcalc> and exits.

=back

=cut

=head1 EXPRESSIONS

=head2 Simple calculations

B<mcalc> can perform standard calculations as you would expect it to work.

    mcalc> 1+1
    2
    mcalc> 4*(7+3)
    40

=head2 Special commands

=over 8

=item I<bye>, I<exit>, I<quit>, 

Exits the program

=back

=head1 SYNTAX

I<statement>    := [ I<identifier> = ] I<expression>

I<expression>   := [ - ] I<term> [ I<operator> I<term> ] ...

I<term>         := I<number> | ( I<expression> ) | I<identifier> [ ( I<args> ) ]

I<args>         := I<expression> [ , I<expression> ] ...

I<identifier>   := regex([a-zA-Z]+)

I<number>       := regex(\d+\.?\d*)

I<operator>     := + | - | * | / | ^

=head1 AUTHOR

Marc Hauptmann <marc.hauptmann@stud.uni-hannover.de>

=cut
